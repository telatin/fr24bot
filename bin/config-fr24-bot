#!/usr/bin/env perl
#ABSTRACT: Configuration for FR24-Bot
#PODNAME: config-fr24-bot

use v5.24;
use warnings;
use Getopt::Long;
use FindBin qw($RealBin);
use Data::Dumper;
use File::Basename;
use JSON::PP;
use Term::ANSIColor qw(:constants);
if (-e "$RealBin/../dist.ini") {
    use lib "$RealBin/../lib";
}
use FR24::Bot;

# Define version
our $VERSION = $FR24::Bot::VERSION;
my $config_file = "$ENV{HOME}/.config/fr24-bot.ini";
my $verbose = 0;
my $opt_api_key;
my $opt_IP;
my $opt_no_write = 0;
if (not GetOptions(
    'a|api-key=s'  => \$opt_api_key,    
    'i|ip=s'       => \$opt_IP,
    'c|config=s'   => \$config_file,
    'no-write'     => \$opt_no_write,
    'verbose'      => \$verbose,
    'version'      => sub { say basename($0). " " . $VERSION; exit 0;},
    'h|help'       => sub { usage(); exit 0;},
)) { 
    usage(); 
    die "ERROR: Invalid options\n";
}

my $config = FR24::Bot::loadconfig($config_file);

# Config to json 

if ($verbose) {
    my $json = JSON::PP->new->utf8->pretty->canonical;
    my $json_text = $json->encode($config);
    say STDERR "------- Config File -------\n", $json_text;
}


my $NEW_API_KEY = undef;
if (not defined $opt_api_key) {
    say STDERR GREEN, "> API KEY", RESET;
    $NEW_API_KEY = getInfoFromUser("Provide API key for Telegram", $config->{"telegram"}->{"apikey"});
} else {

    if ($opt_api_key eq 'default') {
        say STDERR YELLOW, "> Keeping default API KEY: ", $config->{"telegram"}->{"apikey"}, RESET if $verbose;
        $NEW_API_KEY = $config->{"telegram"}->{"apikey"};
    } else {
        say STDERR YELLOW, "> Overwriting API KEY: ", $config->{"telegram"}->{"apikey"}, RESET if $verbose;
        $NEW_API_KEY = $opt_api_key;
    }
}
my $NEW_IP = undef;
if (not defined $opt_IP) {
    say STDERR GREEN, "> FR24 Server IP", RESET;
    $NEW_IP = getInfoFromUser("Provide IP address for FR24", $config->{"server"}->{"ip"});
} else {
    if ($opt_IP eq 'default') {
        say STDERR YELLOW, "> Keeping default IP: ", $config->{"server"}->{"ip"}, RESET if $verbose;
        $NEW_IP = $config->{"server"}->{"ip"};
    } else {
        say STDERR YELLOW, "> Overwriting IP: ", $config->{"server"}->{"ip"}, RESET if $verbose;
        $NEW_IP = $opt_IP;
    }
}

$config->{"telegram"}->{"apikey"} = $NEW_API_KEY if defined $NEW_API_KEY;
$config->{"server"}->{"ip"} = $NEW_IP if defined $NEW_IP;

if ($verbose) {
    my $json = JSON::PP->new->utf8->pretty->canonical;
    my $json_text = $json->encode($config);
    say STDERR "------- New Config File -------\n", $json_text;
}


if ($opt_no_write) {
    say Dumper $config;
    exit 0;
}
eval {
    FR24::Bot::saveconfig($config_file, $config);
};
if ($@) {
    say STDERR "ERROR SAVING to $config_file:\n$@";
    exit 1;
} else {
    say STDERR "Config saved to $config_file" if $verbose;
    exit 0;
}



sub usage {
    say "Usage: $0 [-a|--api-key API_KEY] [-c|--config CONFIG_FILE] [-h|--help] [-v|--version]";
    say "Default config file: $ENV{HOME}/.config/fr24-bot.ini";
}

sub getInfoFromUser {
    my ($prompt, $default) = @_;
    $default = "" if not defined $default;
    print BOLD $prompt, RESET, " [$default]: ";
    my $answer = <STDIN>;
    chomp $answer;
    # Loop to confirm with "y"
    while (1) {
        my $msg = "Do you confirm your answer";
        if (length($answer) < 1) {
            $answer = $default;
            my $msg =  "No answer given. Do you want to keep the default?";
        }

        say UNDERLINE "$msg [$answer]", RESET, BOLD," (y/n): ", RESET;
        my $confirm = <STDIN>;
        chomp $confirm;
        if ($confirm  =~ m/^[yY]$/) {
            return $answer;
        } elsif ($confirm eq "n") {
            return getInfoFromUser($prompt, $default);
        }
    }    
}

__END__


=head1 SYNOPSIS

  config-fr24-bot [OPTIONS]

=head1 DESCRIPTION

The C<config-fr24-bot> script is used to configure the FR24-Bot by modifying the configuration file. 
It provides command-line options to change the API key and FR24 server IP address.

=head1 OPTIONS

The following options are available:

=over 4

=item B<-a, --api-key API_KEY>

Sets the API key for Telegram. If not provided, the script will use an B<interactive> prompt for the API key.

You can specify C<default> to keep the current API key (if any).

=item B<-i, --ip IP>

Sets the FR24 server IP address. If not provided, the script will use an B<interactive> prompt for the IP address.

You can specify C<default> to keep the current API key (if any).

=item B<-c, --config CONFIG_FILE>

Specifies the path to the configuration file. The default configuration file is C<$ENV{HOME}/.config/fr24-bot.ini>.

=item B<--no-write>

Prints the updated configuration without saving it to the file.

=item B<--verbose>

Enables verbose output, showing the original and updated configuration.

=item B<-h, --help>

Displays the help message and usage instructions.

=item B<-v, --version>

Displays the version of the C<config-fr24-bot> script.

=back

=head1 CONFIGURATION FILE

The C<config-fr24-bot> script reads and modifies a configuration file in the INI format. 
The default configuration file is C<$ENV{HOME}/.config/fr24-bot.ini>.

The configuration file has the following sections:

=over 4

=item B<[telegram]>

This section contains the configuration options related to the Telegram API.

It currently has only one option: B<apikey>.

=item B<[server]>

This section contains the configuration options related to the FR24 server.

It currently has only one option: B<ip>.

=back
